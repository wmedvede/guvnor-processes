package defaultPackge


import org.kie.api.runtime.process.WorkflowProcessInstance;

rule "Project URI is not set" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance()
        String(this.isEmpty()) from $processInstance.getVariable("ProjectURI");
    then
        String reason = "Not valid for release as project uri is not given " + $processInstance.getVariable("ProjectURI");
        System.err.println(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release branch and version is not set" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance()
        String(this.isEmpty()) from $processInstance.getVariable("ToReleaseBranch");
        String(this.isEmpty()) from $processInstance.getVariable("ToReleaseVersion");
    then
        String reason = "Not valid for release as branch [1] and/or version [2] is not given "
                               + $processInstance.getVariable("ToReleaseBranch") + ", " + $processInstance.getVariable("ToReleaseVersion");
        System.err.println(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release branch does not start with release" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance()
        String(!this.startsWith('release')) from $processInstance.getVariable("ToReleaseBranch");
    then
        String reason = "Not valid for release as branch to release from does not start with release - given name is "
                                + $processInstance.getVariable("ToReleaseBranch");
        System.err.println(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Check deployment to runtime details" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance()
        Boolean(this == true) from $processInstance.getVariable("DeployToRuntime");
        String(this.isEmpty()) from $processInstance.getVariable("Username");
        String(this.isEmpty()) from $processInstance.getVariable("Password");
        String(this.isEmpty()) from $processInstance.getVariable("ExecServerURL");
    then
        String reason = "Not valid for release as deploy to runtime was selected but no details provided "
                               + $processInstance.getVariable("Username") + ", "+ $processInstance.getVariable("Password")
                                + ", " + $processInstance.getVariable("ExecServerURL");
        System.err.println(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release version ends with SNAPSHOT" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance()
        String(this.endsWith('-SNAPSHOT')) from $processInstance.getVariable("ToReleaseVersion");
    then
        String reason = "Not valid for release as version for release is snapshot - given name is "
                                + $processInstance.getVariable("ToReleaseVersion");
        System.err.println(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end